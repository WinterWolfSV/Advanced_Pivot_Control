//repositories {
//    maven { url 'https://maven.shedaniel.me/' }
//    maven { url "https://maven.terraformersmc.com/releases" }
//
//    mavenCentral()
//}
//

plugins {
    id 'fabric-loom' version '1.3.9'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group
def targetJavaVersion = 17


repositories {
    maven { url 'https://maven.shedaniel.me/' }
    maven { url "https://maven.terraformersmc.com/releases" }

    mavenCentral()
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    // Cloth config
    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    // Mod Menu
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "mod_name": project.mod_name,
                "mod_id": project.mod_id,
                "mod_version": project.version,
                "mod_description": project.mod_description,
                "repository": project.repository,
                "license": project.license,
                "mod_icon": project.mod_icon,
                "environment": project.environment,
                // Below, add all dependencies that you want to add to your mod's fabric.mod.json
                "supported_minecraft_versions": project.supported_minecraft_versions
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.mod_id + project.generated_file_name_addon
    // Uncomment this line to generate a de-obfuscated jar
    // withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
